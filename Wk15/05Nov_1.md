---
title: Principles of Programming Languages (CS1.402)
subtitle: |
          | 05 November, Saturday (Lecture 24)
author: Taught by Prof. Venkatesh Choppella
header-includes: \usepackage{proof}
---

# Continuations (contd.)
We have seen the direct and CPS styles of programming.

## Representation Independence
Consider the CPS-style factorial:
```scheme
(define (fact/c n k)
  (if (= n 0) (k 1)
      (fact/c (sub1 n) (lambda (v)
                         (k (* n v))))))
```

We would like to hide the datatype of $k$ (the fact that it is a function). To this end, instead of calling it, we call a function that applies it indirectly:
```scheme
(define (apply-k k v)
  (k v))
```

We would also like to abstract the $\lambda v$ term in the recursive call. We see that $v$ is the only bound variable in that expression; given $n$ and $k$, therefore, we can construct it. We define a constructor to do this.
```scheme
(define fact1-k
  (lambda (n k)
    (lambda (v)
      (apply-k k (* n v)))))
```

We also wrap the top-level continuation in a closure to make it a constructor (for uniformity):
```scheme
(define top-k
  (lambda ()
    (lambda (v) v)))
```

Thus we have constructors that *produce* continuations, and `apply-k`, which *consumes* continuations.  
Using these constructs, we can define
```scheme
(define (fact/c n k)
  (if (= n 0) (apply-k k 1)
      (fact/c (sub1 n) (fact1-k n k))))

(define (fact n) (fact/c n (top-k)))
```
This code completely hides the underlying representation of continuations – the continuation constructors and `apply-k` form the *interface* of the continuation datatype.

## Non-Procedural Representation
We can define an alternate (non-procedural) representation for continuations.
```scheme
(define-datatype k k?
  [top-k]
  [fact1-k (n nat?) (saved-k k?)])
```

We now need to change the interface to suit this representation.
```scheme
(define (apply-k c v)
  (cases k c
    [top-k () v]
    [fact1-k (n saved-k)
      (apply-k saved-k (* n v))]))
```

This definition of the continuation datatype parallels that of the list type exactly. The way `apply-k` works, additionally, treats this as a stack – each step "pops" a continuation off the top and modifies the accumulator value.

## Closure Representation
A continuation can also be seen as a *closure*, linked with another continuation (in effect, a list of closures).
```scheme
(define-datatype k k?
  [top-k]
  [fact1-k (clo proc?) (saved-k k?)])
```

We now modify the interface for this definition.
```scheme
(define (apply-k c v)
  (cases k c
    [top-k () v]
    [fact1-k (clo saved-k)
      (apply-k saved-k (clo v))]))
```

Now we define the factorial function as
```scheme
(define (fact/c n c)
  (if (= n 0) (apply-k c 1)
      (fact/c (sub1 n)
              (fact1-k (lambda (v) (* n v)) k))))
```